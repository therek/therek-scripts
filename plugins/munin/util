#!/usr/bin/perl -w
#
# Plugin for watching percentage of CPU time during which
# I/O requests were issued to the device
# Usage: Link or copy into /etc/lrrd/client.d/
#
# Parameters:
#
# 	config   (required)
# 	autoconf (optional - used by lrrd-config)
#	reset
#
# version 1.1:
# Fixed problems with negative values, when previous uptime > current uptime
#
# version 1.0:
# First version (the scripts reads from /proc/diskstat only, for now)
#
#%# family=auto
#%# capabilities=autoconf

use strict;

###################
# global variables
#
my $tmpfile = '/tmp/munin-util.tmp';
my $procfile = '/proc/diskstats';

my (%putil, %cutil, $cuptime, $firstrun);

######################################################
# subroutines:
# - read_previous reads previous values from $tmpfile
# - read_current reads current values from $procfile
#
sub read_previous {
	if (-e $tmpfile) {
		$firstrun = '0';
		open (TMP, "$tmpfile") or die "Could not open $tmpfile for reading: $!\n";
		while (<TMP>) {
			my($name, $util, $uptime) = $_ =~ /^(.+?)\s+(\d+)\s+(\d+)$/;
			$putil{"$name"} = {
				util => $util,
				uptime => $uptime
			}
		}
		close (TMP);
	} else { $firstrun = '1' }
}

sub read_current {
	open (PROC, "$procfile") or die "Could not open $procfile for reading: $!\n";
	while (<PROC>) {
		my $count = '0';
		my @cols = split /\s+/;
		$count ++ foreach (@cols);
		next unless $count == 15;
		my($name, $rio, $util) = $_ =~ /^\s*\d+\s+\d+\s+(.+?)\s+(\d+) \d+ \d+ \d+ \d+ \d+ \d+ \d+ \d+ (\d+) \d+$/;
		next unless ($rio != 0);
		$cutil{"$name"} = {
			name => $name,
			util => $util
		}
	}
	close (PROC);
}

#############################
# parse command line options
# - autoconf
# - config
# - reset
#
if ( $ARGV[0] and $ARGV[0] eq "autoconf") {
	if (-e "$procfile" ) {
		print "yes\n";
		exit 0;
	}
	print "no\n";
	exit 1;
}

if ( $ARGV[0] and $ARGV[0] eq "reset") {
	unless (-e $tmpfile ) {
		print "No need to reset, the $tmpfile does not exists\n";
		exit 1;
	}
	unlink $tmpfile;
	if (-e $tmpfile) {
		print "shit! please remove $tmpfile manually\n";
		exit 1;
	}
	print "$tmpfile removed\n";
	exit 0;
}

if ( $ARGV[0] and $ARGV[0] eq "config") {
	&read_current;
	print "graph_title Percentage of CPU time utilized by IO requests\n";
	print "graph_vlabel \%util\n";
	print "graph_category disk\n";
	print "graph_info Percentage of CPU time during which I/O requests were issued to the device.\n";
	foreach (sort keys %cutil) {
		my $printkey = $_;    # rrd had problems generating graphs when
		$printkey =~ s/-/_/g; # the name contained a '-' character
		print "_dev_$printkey.label $cutil{$_}->{name} \n";
		print "_dev_$printkey.info \%util for $cutil{$_}->{name} \n";
	}
	exit 0;
}


#######################
# show what you've got
#
&read_previous;
&read_current;

$cuptime=`cat /proc/uptime | cut -d"." -f1`;
chomp($cuptime);

foreach (sort keys %cutil) {
	my ($util, $upt);
	if ( $firstrun == '1' || $cuptime < $putil{$_}->{uptime} ) { # if previous uptime
		$util = '0';     # is bigger than current it means that the tempfile has
		$upt = $cuptime; # not been removed while the system rebooted
	} else {
		$util = $cutil{$_}->{util} - $putil{$_}->{util};
		$upt = $cuptime - $putil{$_}->{uptime};
	}
	my $tps = $util / $upt * 0.1024;
	my $printkey = $_;    # rrd had problems generating graphs when
	$printkey =~ s/-/_/g; # the name contained a '-' character
	printf "_dev_$printkey.value %.2f\n", $tps;

}

###############################################
# write current values (overwritting previous)
#
open (TMP, ">", "$tmpfile") or die "Could not open $tmpfile for writing: $!\n";
foreach (sort keys %cutil) {
	print TMP "$_ $cutil{$_}->{util} $cuptime\n";
}
close (TMP);

# and that's all
# hopefully this will work on all 2.6 kernels
#
# vim:syntax=perl
